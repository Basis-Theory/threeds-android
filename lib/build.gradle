plugins {
    id 'com.android.library'
    alias(libs.plugins.jetbrains.kotlin.android)
    id "org.jetbrains.kotlin.plugin.serialization" version "2.0.21"
    id "maven-publish"
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId "com.basistheory"
            artifactId "3ds-android"
            version = project.hasProperty('version') ? project.version : 'default-version'
            artifact("$buildDir/outputs/aar/lib-release.aar")
        }

    }

    repositories {
        maven {
            name "GithubPackages"
            url uri("https://maven.pkg.github.com/basis-theory/3ds-android")

            credentials {
                username = "platform@basistheory.com"
                password = properties["GITHUB_TOKEN"] ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

android {
    namespace 'com.basistheory.threeds'
    compileSdk 34

    defaultConfig {
        minSdk 21
        targetSdk 34
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug {
            debuggable true
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
}

configurations.all {
    resolutionStrategy {
        force 'io.netty:netty-codec-http2:4.1.100.Final'
        force 'com.google.protobuf:protobuf-java:3.25.5'
        force 'com.nimbusds:nimbus-jose-jwt:9.37.2'
        force 'net.minidev:json-smart:2.4.9'
    }
}

dependencies {
    implementation "com.ravelin.threeds2service:threeds2service-sdk:1.4.2"

    implementation libs.androidx.core.ktx

    implementation 'com.squareup.okhttp3:okhttp:4.11.0'
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.7.1"

}